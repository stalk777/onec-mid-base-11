
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//НачалоДоработки
	//Воронов А.В, ДомашниеЗадание "Внесение изменений в конфигурацию на поддержке" от 08.02.2024
	//Добавления поля ввода размера скидки и команды перерасчета скидки на форму
	//
	////Команда
	Команда = Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "ПересчитатьТаблицу";
	Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Команда.Картинка = БиблиотекаКартинок.Обновить;
	Команда.Действие = "КомандаПересчитатьТаблицу";
	//
	////Группа Формы
	Группа = Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"), ЭтаФорма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	//
	////Реквезит на Форму
	ПолеВвода = Элементы.Добавить("ЭлементСогласованнаяСкидка", Тип("ПолеФормы"), Группа);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДР_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении","СогласованнаяСкидкаПриИзменении");
	//
	///Каманда на Форму
	КнопкаФормы = Элементы.Добавить("КомандаПересчитатьТаблицу", Тип("КнопкаФормы"), Группа);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	//
	//КонецДоработки
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//НачалоДоработки
	//Воронов А.В, ДомашниеЗадание "Механизм поддержки, обновление" от 21.02.2024
	//Объединение скидок (форма и табличная часть) и включение их в сумму товаров.
	//
	//Исходный код:
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	//
	//Новый код:
	ДР_РассчитатьСуммуСтроки(ТекущиеДанные);
	//
	//КонецДоработки

КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти


#Область Доработки

&НаКлиенте
Процедура СогласованнаяСкидкаПриИзменении(Элемент)
	//НачалоДоработки
	//Воронов А.В, ДомашниеЗадание "Внесение изменений в конфигурацию на поддержке" от 08.02.2024
	//Скидка. Проверка заполнености ТЧ Товаров и Услуг.
	//
	Если Объект.Товары.Количество() > 0 ИЛИ Объект.Услуги.Количество() > 0 Тогда
		ЗадатьВопросОПродолжении();
	Иначе
		Возврат;
	КонецЕсли;
	//
	//КонецДоработки
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗадатьВопросОПродолжении()
	//НачалоДоработки
	//Воронов А.В, ДомашниеЗадание "Внесение изменений в конфигурацию на поддержке" от 08.02.2024
	//Скидка. Диалог о пересчёте суммы.
	//
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Изменён процент скидки. Пересчитать таблицу товаров и услуг?';"
		+ " en = 'The discount percentage has been changed. Recalculate the table of goods and services?'"), Режим, 0);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ДР_ПересчитатьТоварыИУслуги();
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	//
	//КонецДоработки
КонецПроцедуры

&НаКлиенте
Процедура КомандаПересчитатьТаблицу(Команда)
	//НачалоДоработки
	//Воронов А.В, ДомашниеЗадание "Внесение изменений в конфигурацию на поддержке" от 08.02.2024
	//Скидка. Интерактивный пересчет суммы.
	//
	ДР_ПересчитатьТоварыИУслуги();
	//
	//КонецДоработки
КонецПроцедуры

&НаКлиенте
Процедура ДР_ПересчитатьТоварыИУслуги()
	//НачалоДоработки
	//Воронов А.В, ДомашниеЗадание "Внесение изменений в конфигурацию на поддержке" от 08.02.2024
	//Скидка. Пересчёт суммы по каждой позиции ТЧ Товаров и Услуг.
	//
	Для Каждого Строка Из Объект.Товары Цикл
		ДР_РассчитатьСуммуСтроки(Строка);
	КонецЦикла;
	Для Каждого Строка Из Объект.Услуги Цикл
		ДР_РассчитатьСуммуСтроки(Строка);
	КонецЦикла;
	//
	//КонецДоработки
КонецПроцедуры

&НаКлиенте
Процедура ДР_РассчитатьСуммуСтроки(ТекущиеДанные)
	//НачалоДоработки
	//Воронов А.В, ДомашниеЗадание "Механизм поддержки, обновление" от 19.02.2024
	//Скидка. Объединение скидок (форма и табличная часть) и включение их в сумму товаров.
	КофициентСкидки = (1 - (Объект.ДР_СогласованнаяСкидка + ТекущиеДанные.Скидка)/100);
	//
	Если КофициентСкидки <= 0 Тогда
		Сообщить("Скидка 100%");
		ТекущиеДанные.Сумма = 0;
	Иначе
		ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество*КофициентСкидки;
	КонецЕсли;
	РассчитатьСуммуДокумента();
	//
	//КонецДоработки
КонецПроцедуры

#КонецОбласти


